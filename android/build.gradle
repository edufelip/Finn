// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
  repositories {
    google()
    mavenCentral()
  }
  dependencies {
    classpath('com.android.tools.build:gradle:8.5.2')
    classpath('com.facebook.react:react-native-gradle-plugin')
    classpath('org.jetbrains.kotlin:kotlin-gradle-plugin')
    classpath('com.google.gms:google-services:4.4.1')
    classpath('com.google.firebase:firebase-crashlytics-gradle:2.9.9')
  }
}

allprojects {
  repositories {
    google()
    mavenCentral()
    maven { url 'https://www.jitpack.io' }
  }
}

def preferredNdkVersion = findProperty("android.ndkVersion")
if (preferredNdkVersion) {
  rootProject.ext.ndkVersion = preferredNdkVersion
}

apply plugin: "expo-root-project"
apply plugin: "com.facebook.react.rootproject"

// Ensure Reanimated's CMake config waits for Worklets Prefab package generation.
gradle.projectsEvaluated {
  def workletsProject = rootProject.findProject(":react-native-worklets")
  def reanimatedProject = rootProject.findProject(":react-native-reanimated")
  if (workletsProject == null || reanimatedProject == null) {
    return
  }

  def prefabRelease = workletsProject.tasks.findByName("prefabReleasePackage")
  if (prefabRelease != null) {
    reanimatedProject.tasks.matching { it.name.startsWith("configureCMakeRelWithDebInfo") }.configureEach {
      dependsOn(prefabRelease)
    }
  }

  def prefabDebug = workletsProject.tasks.findByName("prefabDebugPackage")
  if (prefabDebug != null) {
    reanimatedProject.tasks.matching { it.name.startsWith("configureCMakeDebug") }.configureEach {
      dependsOn(prefabDebug)
    }
  }
}
